#!/usr/bin/perl -w

#
# File:
#   ag_smt_staging
#
# Authors:
#   Lukas Ocilka <locilka@suse.cz>
#
# Description:
#   SCR agent for repositories staging management
#
# $Id$
#


use lib "/usr/lib/YaST2/agents_non_y2";
use ycp;
use strict;

use SMT::Utils;
use SMT::CLI;
use SMT::Filter;
use SMT::Repositories;
use SMT::Parser::RpmMdPatches;
use SMT::Mirror::RpmMd;

# For debugging only
# use Data::Dumper;

# Suppress printing logs to STDOUT, to nemaji kralicci radi
# (SCR uses stdin/stdout for communication with agents)
SMT::Utils::setLogBehavior ({'doprint' => 0});

if(! SMT::Utils::dropPrivileges()) {
    y2error ('Unable to drop privileges. Aborting...');
    ycp::Return (undef);
    exit 1;
}

# Initialize SMT
my ($cfg, $dbh, $nuri) = SMT::CLI::init();
my $base_path = $cfg->val('LOCAL', 'MirrorTo') || '/srv/www/htdocs';

# Used for re-signing repositories (during staging)
my $keyid	= $cfg->val('LOCAL', 'signingKeyID')		|| '';
my $passphrase	= $cfg->val('LOCAL', 'signingKeyPassphrase')	|| '';

my $repositories = SMT::Repositories::new($dbh);

use constant {
    YCP_TRUE	=> 'true',
    YCP_FALSE	=> 'false',

    # All Mirrored repositories with Staging feature enabled
    MIRRORED_REPO_FILTER => {
	SMT::Repositories::MIRRORING	=> SMT::Repositories::MIRRORING_TRUE,
	SMT::Repositories::STAGING	=> SMT::Repositories::STAGING_TRUE,
    },

    # All Mirrored repositories with Staging feature enabled 
    ALL_REPO_FILTER => {
	SMT::Repositories::MIRRORABLE	=> SMT::Repositories::MIRRORABLE_TRUE,
    },
};

my $log = SMT::Utils::openLog ('/var/log/smt/smt-staging.log');
my $vblevel = LOG_DEBUG|LOG_DEBUG2|LOG_WARN|LOG_ERROR|LOG_INFO1|LOG_INFO2;

# Reports that the agent has been called using an unsupported path
sub UnsupportedPath ($$) {
    my ($command_ref, $path_ref) = (shift, shift);

    y2error ('Unsupported path: '.$$path_ref.' for command '.$$command_ref);
    ycp::Return (undef);
}

# Filters and Patches Handles
my $fph = {};

# Initializes required handles (in not yet initialized)
# loads filters
sub LoadRepositoryData ($) {
    my $repositoryid = shift || "";

    # No repository ID defined, unable to load data
    if ($repositoryid eq "") {
	y2error ("Repository ID must be defined");
	return undef;
    }

    # Already loaded
    if (defined $fph->{$repositoryid}) {
	return 1;
    }

    my $ret = 1;

    # New empty Handler
    $fph->{$repositoryid} = {};

    # New Filter handler
    $fph->{$repositoryid}->{'fh'} = SMT::Filter->new();
    if (! $fph->{$repositoryid}->{'fh'}->load($dbh, $repositoryid)) {
	y2error ("Cannot load filters for repository: ".$repositoryid);
	$ret = 0;
    }

    # Repository path relative to the base-path
    my $repo_path_suffix = $repositories->getRepositoryPath($repositoryid) || do {
	y2error ('Unable to load repository data for: '.$repositoryid);
	return;
    };

    # repo/$REPOSITORY
    # repo/testing/$REPOSITORY
    # repo/full/$REPOSITORY
    $fph->{$repositoryid}->{'paths'} = {
	# The base local path to all repos
	'base'		=> $base_path.'/repo/',
	# Particular subrepositories
	# * 'full' contains the mirrored data
	'full'		=> 'full/'.$repo_path_suffix,
	# * 'testing' is a snapshot intended for testing
	'testing'	=> 'testing/'.$repo_path_suffix,
	# * 'production' is a snapshot intended for production
	'production'	=> $repo_path_suffix,
    };

    my $repository_path = $fph->{$repositoryid}->{'paths'}->{'base'}.$fph->{$repositoryid}->{'paths'}->{'full'};
    y2milestone ("Parsing local repository: ".$repository_path);

    # The following lines should go to a module in SMT package
    # New Patches-Parser handler
    $fph->{$repositoryid}->{'ph'} = SMT::Parser::RpmMdPatches->new();
    $fph->{$repositoryid}->{'ph'}->resource($repository_path);
    $fph->{$repositoryid}->{'ph'}->parse("repodata/updateinfo.xml.gz", "repodata/patches.xml");

    return $ret;
}

# Goes through all the SMT::Filter handles and saves the data
# repository by repository
sub WritePatchFilters () {
    my $repositoryid = '';

    my $ret = 1;

    # Runs through all already loaded repositories
    foreach $repositoryid (keys %{$fph}) {
	y2milestone ("Calling save() on Catalog:".$repositoryid);
	if (! $fph->{$repositoryid}->{'fh'}->save($dbh, $repositoryid)) {
	    y2error ("Error saving catalog ".$repositoryid);
	    $ret = 0;
	}
    }

    return ($ret ? YCP_TRUE:YCP_FALSE);
}

# Returns patches with their status
sub GetAllPatches ($) {
    my $repositoryid = shift || "";

    if ($repositoryid eq "") {
	y2error ("Catalog ID must be defined");
	return undef;
    }

    y2milestone ("Handling repositoryid: ".$repositoryid);

    LoadRepositoryData($repositoryid);

    my @ret;
    my $patchname = "";
    my $tmppatch = {};

    foreach my $patchid (keys %{$fph->{$repositoryid}->{'ph'}->{'PATCHES'}}) {
	$tmppatch = $fph->{$repositoryid}->{'ph'}->{'PATCHES'}->{$patchid};
	$tmppatch->{'patchid'} = $patchid;
	$tmppatch->{'filtered'} = ($fph->{$repositoryid}->{'fh'}->matches($tmppatch) ? YCP_TRUE:YCP_FALSE);
	push @ret, $tmppatch;
    }

    return \@ret;
}

# Changes the patch status by adding or removing TYPE_NAME_VERSION filter
# Patches can be matching another filters though
sub ChangePatchStatus ($) {
    my $params = shift;

    my $repositoryid	= $params->{'repositoryid'}	|| '';
    my $patchid		= $params->{'patchid'}		|| '';
    my $new_status	= $params->{'status'}		|| '';

    if ($repositoryid eq '') {
	y2error ("Parameter 'repositoryid' not defined");
	return YCP_FALSE;
    }
    if ($patchid eq '') {
	y2error ("Parameter 'patchid' not defined");
	return YCP_FALSE;
    }

    if (! GetStagingAllowedInternal({'repositoryid' => $repositoryid})) {
	y2error ('Repository '.$repositoryid.' does not allow filtering');
    }

    LoadRepositoryData($repositoryid);

    # true -> wanted in testig repository
    if ($new_status) {
	$fph->{$repositoryid}->{'fh'}->remove(SMT::Filter::TYPE_NAME_VERSION, $patchid);
    # false -> not wanted
    } else {
	$fph->{$repositoryid}->{'fh'}->add(SMT::Filter::TYPE_NAME_VERSION, $patchid);
    }

    return YCP_TRUE;
}

# Changes status of a filter by category
sub ChangeCategoryFilterStatus ($) {
    my $params = shift;

    my $repositoryid	= $params->{'repositoryid'}	|| '';
    my $type		= $params->{'type'}		|| '';
    my $new_status	= $params->{'status'}		|| '';

    if ($repositoryid eq '') {
	y2error ("Parameter 'repositoryid' not defined");
	return YCP_FALSE;
    }
    if ($type eq '') {
	y2error ("Parameter 'type' not defined");
	return YCP_FALSE;
    }

    LoadRepositoryData($repositoryid);

    # true -> filter is required
    if ($new_status) {
	$fph->{$repositoryid}->{'fh'}->add(SMT::Filter::TYPE_SECURITY_LEVEL, $type);
    # false -> remove filter
    } else {
	$fph->{$repositoryid}->{'fh'}->remove(SMT::Filter::TYPE_SECURITY_LEVEL, $type);
    }

    return YCP_TRUE;
}

# Returns whether patch is enabled in testing repository
sub GetPatchStatus ($) {
    my $params = shift;

    my $repositoryid	= $params->{'repositoryid'}	|| '';
    my $patchid		= $params->{'patchid'}		|| '';

    if ($repositoryid eq '') {
	y2error ("Parameter 'repositoryid' not defined");
	return undef;
    }
    if ($patchid eq '') {
	y2error ("Parameter 'patchid' not defined");
	return undef;
    }

    LoadRepositoryData($repositoryid);

    my $this_patch = $fph->{$repositoryid}->{'ph'}->{'PATCHES'}->{$patchid};
    # The current patch status (in testing repo) -> ! matching (filters)
    return ($fph->{$repositoryid}->{'fh'}->matches($this_patch) ? YCP_FALSE:YCP_TRUE);
}

# Returns whether a given filter exists
#
# $param->{'repositoryid'} - hash to identyfy a catalog
# $param->{'type'} - one of the well known types
# $param->{'filter'} - filter string
sub FilterExists ($) {
    my $params = shift;

    my $repositoryid	= $params->{'repositoryid'}	|| '';
    my $type		= $params->{'type'}		|| '';
    my $filter		= $params->{'filter'}		|| '';

    if ($repositoryid eq '') {
	y2error ("Parameter 'repositoryid' not defined");
	return undef;
    }
    if ($type eq '') {
	y2error ("Parameter 'type' not defined");
	return undef;
    }
    if ($filter eq '') {
	y2error ("Parameter 'filter' not defined");
	return undef;
    }

    LoadRepositoryData($repositoryid);
    return $fph->{$repositoryid}->{'fh'}->contains($type, $filter);
}

# Returns whether filter by patch type exists (is active)
sub SecurityLevelFilterExists ($) {
    my $arg = shift;
    my $repositoryid = $arg->{'repositoryid'} || do {
	y2error ("Parameter 'repositoryid' not defined");
	return undef;
    };

    LoadRepositoryData($repositoryid);

    my $func_args = {
	'type' => SMT::Filter::TYPE_SECURITY_LEVEL,
	# filter string is the security level of a patch
	'filter' => (defined $arg->{'type'} ? $arg->{'type'}:''),
	'repositoryid' => $repositoryid,
    };

    return (FilterExists ($func_args) ? YCP_TRUE:YCP_FALSE);
}

sub CreateSnapshot ($) {
    my $arg = shift || {};

    # Identifies the repository
    my $repositoryid = $arg->{'repositoryid'} || do {
	y2error ("Parameter 'repositoryid' not defined");
	return YCP_FALSE;
    };

    # Defines a type of generated subrepository
    my $type = $arg->{'type'} || do {
	y2error ("Parameter 'type' not defined");
	return YCP_FALSE;
    };

    # Fills up also $fph->{$repositoryid}->{'paths'}
    LoadRepositoryData($repositoryid);

    # 'testing' or 'production' or ...
    if (not defined $fph->{$repositoryid}->{'paths'}->{$type}) {
	y2error ("Unknown 'type': ".$type);
	return YCP_FALSE;
    }

    # Optional parameter, if a passphrase is defined, we use it and remember
    if (defined $arg->{'passphrase'}) {
	y2milestone ("Using passphrase defined as a parameter");
	$passphrase = $arg->{'passphrase'};
    }

    my %mirror_args = (
	log => $log,
	vblevel => $vblevel
    );

    my $staging_allowed = GetStagingAllowedInternal ({'repositoryid' => $repositoryid});

    # Use filters only if staging is allowed
    if ($staging_allowed) {
	$mirror_args{'filter'} = $fph->{$repositoryid}->{'fh'};
    } else {
	y2milestone ('Filtering is not allowed, not using filters');
    }

    # The default source
    my $staging_source = $fph->{$repositoryid}->{'paths'}->{'base'}.$fph->{$repositoryid}->{'paths'}->{'full'};

    # If there are no filters allowed, we create the production snapshot from the testing one
    # because the testing repository is actually the one we want to have in production snapshot
    if (!$staging_allowed && $type eq 'production') {
	my $check_source = $fph->{$repositoryid}->{'paths'}->{'base'}.$fph->{$repositoryid}->{'paths'}->{'testing'};

	# Testing snapshot exists and is not empty
	if (-e $check_source && -d $check_source && -s $check_source) {
	    y2milestone ('Using \'testing\' repository as source for \'production\'');
	    $staging_source = $check_source;
	# Using fallback (default one)
	} else {
	    y2warning ('Cannot use \'testing\' repository as a source for \'production\', using \'full\'');
	}
    }

    my $mirror = SMT::Mirror::RpmMd->new(%mirror_args);

    # The source of data
    y2milestone ('Source: '.$staging_source);
    $mirror->uri('file://'.$staging_source);

    # Where the resulting subrepository is created
    y2milestone (
	'Base: '.$fph->{$repositoryid}->{'paths'}->{'base'}.' '.
	'Repo: '.$fph->{$repositoryid}->{'paths'}->{$type}.' '.
	'Type: ('.$type.')'
    );
    $mirror->localBasePath($fph->{$repositoryid}->{'paths'}->{'base'});
    $mirror->localRepoPath($fph->{$repositoryid}->{'paths'}->{$type});

    # And GO!
    my $ret = $mirror->mirror ($keyid, $passphrase);

    # Contains number of errors
    return ($ret == 0 ? YCP_TRUE:YCP_FALSE);
}

# Cache for 'stagingAllowed()' results
my $st_allowed_cache = {};

sub GetStagingAllowedInternal ($) {
    my $arg = shift || {};

    my $repositoryid = $arg->{'repositoryid'} || do {
	y2error ('Parameter repositoryid must be defined');
	return undef;
    };

    # Purge the old result to recheck
    if (defined $arg->{'force_check'} && defined $st_allowed_cache->{$repositoryid}) {
	y2milestone ('Purging cached info for repository '.$repositoryid.' on request');
	$st_allowed_cache->{$repositoryid} = undef;
    }

    my $allowed = 0;
    my $errors = '';

    if (defined $st_allowed_cache->{$repositoryid}) {
	$allowed = $st_allowed_cache->{$repositoryid};
    } else {
	# Clear all the previous errors
	$repositories->getAndClearErrorMessage();
	$allowed = $repositories->stagingAllowed($repositoryid, $base_path);
	$errors = $repositories->getAndClearErrorMessage();
	# Cache the result
	$st_allowed_cache->{$repositoryid} = $allowed;
    }

    if ($errors) {
	foreach my $eline (split (/\n/, $errors)) {
	    y2error ("stagingAllowed: ".$eline);
	}
    }

    return $allowed;
}

sub GetStagingAllowed ($) {
    my $arg = shift || {};

    return (GetStagingAllowedInternal($arg) ? YCP_TRUE:YCP_FALSE);
}

# the main() loop
while (<STDIN>) {
    my ($command, $path, $arg) = ycp::ParseCommand ($_);
    # Just for debugging
    # y2internal ('Command:'.Dumper($command).' Path:'.Dumper($path).' Arg:'.Dumper($arg));

    # For Perl-only testing, otherwise already removed
    if ($path =~ /^\.smt/) {
	$path =~ s/^\.smt//;
    }

    if ($command eq 'Read') {
	if ($path eq '.staging.repositories') {
	    ycp::Return ($repositories->getAllRepositories (MIRRORED_REPO_FILTER));
	} elsif ($path eq '.staging.patches') {
	    ycp::Return (GetAllPatches ($arg));
	} elsif ($path eq '.staging.patch.testing') {
	    ycp::Return (GetPatchStatus ($arg));
	} elsif ($path eq '.staging.category_filter') {
	    ycp::Return (SecurityLevelFilterExists ($arg));
	} elsif ($path eq '.repositories.all') {
	    ycp::Return ($repositories->getAllRepositories (ALL_REPO_FILTER));
	} elsif ($path eq '.repository.staging_allowed') {
	    ycp::Return (GetStagingAllowed ($arg));
	} else {
	    UnsupportedPath (\$command, \$path);
	}
    } elsif ($command eq 'Write') {
	if ($path eq '.staging.patch.testing') {
	    ycp::Return (ChangePatchStatus ($arg));
	} elsif ($path eq '.staging.patches') {
	    ycp::Return (WritePatchFilters());
	} elsif ($path eq '.staging.category_filter') {
	    ycp::Return (ChangeCategoryFilterStatus ($arg));
	} elsif ($path eq '.staging.snapshot') {
	    ycp::Return (CreateSnapshot ($arg));
	} else {
	    UnsupportedPath (\$command, \$path);
	}
    } elsif ($command eq 'Execute') {
	    UnsupportedPath (\$command, \$path);
	    ycp::Return (undef);
    # destructor
    } elsif ($command eq 'result') {
	y2milestone ('Bye bye...');
	last;
    } else {
	y2error ('Unknown SCR command: '.$command);
	ycp::Return (undef);
    }
}
# the main() loop

exit 0;
