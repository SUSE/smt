/**
 * File:	clients/smt.ycp
 * Package:	Configuration of smt
 * Summary:	Definition of dialog sequences
 * Authors:	Lukas Ocilka <locilka@suse.cz>
 *
 * $Id: wizard.ycp 27914 2006-02-13 14:32:08Z locilka $
 *
 * Main file for smt configuration. Uses all other files.
 */

{
    textdomain "smt";

    import "CWMTab";
    import "Sequencer";
    import "CWM";
    import "Mode";
    import "CWMFirewallInterfaces";

    include "smt/helps.ycp";
    include "smt/dialogs.ycp";

    map <string, map <string, any> > widgets = $[
	    "cr" : $[
		"widget"	: `custom,
		"help"		: HELPS["credentials"]:"",
		"custom_widget"	: `Empty(),
		"handle"	: HandleCredentialsDialog,
		"init"		: InitCredentialsDialog,
		"store"		: StoreCredentialsDialog,
		"validate_type"	: `function,
		"validate_function"	: ValidateCredentialsDialog,
	    ],
	    "db" : $[
		"widget"	: `custom,
		"help"		: HELPS["database"]:"",
		"custom_widget"	: `Empty(),
		"validate_type"	: `function,
		"validate_function"	: ValidateDatabaseDialog,
		"init"		: InitDatabaseDialog,
		"store"		: StoreDatabaseDialog,
	    ],
	    "sd" : $[
		"widget"	: `custom,
		"help"		: HELPS["scheduled_downloads"]:"",
		"custom_widget"	: `Empty(),
		"handle"	: HandleScheduledDownloadsDialog,
		"init"		: InitScheduledDownloadsDialog,
		"store"		: StoreScheduledDownloadsDialog,
	    ],
	    "reporting" : $[
		"widget"	: `custom,
		"help"		: HELPS["reporting"]:"",
		"custom_widget"	: `Empty(),
		"handle"	: HandleReportEmailTableDialog,
		"init"		: InitReportEmailTableDialog,
		"store"		: StoreReportEmailTableDialog,
	    ],
	    "firewall" : CWMFirewallInterfaces::CreateOpenFirewallWidget ($[
		// renamed in SLES11
		"services" : [ "service:apache2", "service:apache2-ssl" ],
		"display_details" : false,
	    ]),
	    "repositories" : $[
		"widget"	: `custom,
		"help"		: HELPS["repositories"]:"",
		"custom_widget"	: `Empty(),
		"handle"	: HandleRepositoriesTableDialog,
		"init"		: InitRepositoriesTableDialog,
		// "store"	: StoreRepositoriesTableDialog,
	    ],
	    "staging" : $[
		"widget"	: `custom,
		"help"		: HELPS["staging"]:"",
		"custom_widget"	: `Empty(),
		"handle"	: HandleStagingTableDialog,
		"init"		: InitStagingTableDialog,
		// "store"	: StoreStagingTableDialog,
	    ],
    ];

    map <string, any> tabs = $[
	    "credentials" : $[
		// TRANSLATORS: tab-header
		"header"	: _("Customer Center Access"),
		"widget_names"	: ["cr", "firewall"],
		"contents"	: CredentialsDialogContent(),
	    ],
	    "database" : $[
		// TRANSLATORS: tab-header
		"header"	: _("Database and Reporting"),
		"widget_names"	: ["db", "reporting"],
		"contents"	:   `VBox (
					DatabaseDialogContent(),
					`VSpacing (1),
					ReportEmailTableContent(),
					`VStretch()
				    ),
	    ],
	    "repositories" : $[
		// TRANSLATORS: tab-header
		"header"	: _("Repositories"),
		"widget_names"	: ["repositories"],
		"contents"	: CatalogsTableContent(),
	    ],
	    "staging" : $[
		// TRANSLATORS: tab-header
		"header"	: _("Staging"),
		"widget_names"	: ["staging"],
		"contents"	: StagingTableContent(),
	    ],
	    "scheduled_downloads" : $[
		// TRANSLATORS: tab-header
		"header"	: _("Scheduled SMT Jobs"),
		"widget_names"	: ["sd"],
		"contents"	: ScheduledDownloadsDialogContent(),
	    ],
    ];

    symbol InstallCredentialsDialog () {
	list<map<string,any> > w = CWM::CreateWidgets (tabs["credentials", "widget_names"]:[], widgets);
	term contents = CWM::PrepareDialog (tabs["credentials", "contents"]:`Empty(), w);
	string caption = sformat (_("SMT Configuration Wizard - Step %1/%2"), 1, 2);
	string help = CWM::MergeHelps (w);

	Wizard::SetContentsButtons (caption, contents, help,
	    Label::BackButton (), Label::NextButton ());
	return CWM::Run (w, $[]);
    }

    symbol InstallDatabaseDialog () {
	list<map<string,any> > w = CWM::CreateWidgets (tabs["database", "widget_names"]:[], widgets);
	term contents = CWM::PrepareDialog (tabs["database", "contents"]:`Empty(), w);
	string caption = sformat (_("SMT Configuration Wizard - Step %1/%2"), 2, 2);
	string help = CWM::MergeHelps (w);

	Wizard::SetContentsButtons (caption, contents, help,
	    Label::BackButton (), Label::NextButton ());
	return CWM::Run (w, $[]);
    }

    any MainSequence (string sequence_type) {
	map <string, any> wd = $[];
	string caption = "";

	if (sequence_type == "config") {
	    // TRANSLATORS: dialog caption
	    caption = _("NU Configuration");
	    wd = $[
		"tab" : CWMTab::CreateWidget ($[
		    "tab_order"	: ["credentials", "database", "scheduled_downloads"],
		    "tabs"		: tabs,
		    "widget_descr"	: widgets,
		    "initial_tab"	: "credentials",
		])
	    ];
	} else if (sequence_type == "management") {
	    // TRANSLATORS: dialog caption
	    caption = _("SMT Management");
	    wd = $[
		"tab" : CWMTab::CreateWidget ($[
		    "tab_order"	: ["repositories", "staging"],
		    "tabs"		: tabs,
		    "widget_descr"	: widgets,
		    "initial_tab"	: "repositories",
		])
	    ];
	} else {
	    y2error ("Unknown sequence_type: %1", sequence_type);
	}

	term contents = `VBox("tab");
	list <map <string, any> > w = CWM::CreateWidgets (["tab"], (map <string, map <string, any> >) wd);
	contents = CWM::PrepareDialog (contents, w);

	Wizard::SetContentsButtons (
	    caption,
	    contents,
	    "",
	    Label::BackButton (),
	    Label::OKButton()
	);
	Wizard::DisableBackButton();
	Wizard::SetAbortButton(`abort, Label::CancelButton());

	Wizard::SetTitleIcon("yast-smt");

	return CWM::Run (w, $[`abort : ReallyExit]);
    }

    any MainInstallSequence() {
	map aliases = $[
	    "credentials" : ``( InstallCredentialsDialog() ),
	    "database" : ``( InstallDatabaseDialog() ),
	];

	map sequence = $[
	    "ws_start" : "credentials",
	    "credentials" : $[
		`abort      : `abort,
		`next       : "database",
	    ],
	    "database" : $[
		`abort      : `abort,
		`next       : `next,
	    ],
	];

	Wizard::SetTitleIcon ("yast-smt");

	any ret = Sequencer::Run(aliases, sequence);

	return ret;
    }

    any SMTSequence () {
	map aliases = $[
	    "read"  : [ ``( ReadDialog() ), true ],
	    "main"  :   ``( MainSequence ("config") ),
	    "check" : [ ``( CheckConfigDialog() ), true ],
	    "write" : [ ``( WriteDialog() ), true ]
	];

	map sequence = $[
	    "ws_start" : "read",
	    "read" : $[
		`abort	: `abort,
		`next	: "main"
	    ],
	    "main" : $[
		`abort	: `abort,
		`next	: "check"
	    ],
	    "check" : $[
		`abort	: `abort,
		`next	: "write",
	    ],
	    "write" : $[
		`abort      : `abort,
		`next       : `next
	    ],
	];

	Wizard::CreateDialog();
	Wizard::DisableBackButton();
	Wizard::SetAbortButton(`abort, Label::CancelButton());
	Wizard::SetNextButton(`next, Label::OKButton());
	Wizard::SetTitleIcon ("yast-smt");

	any ret = Sequencer::Run(aliases, sequence);
	Wizard::CloseDialog();

	return ret;
    }

    any SMTManagementSequence () {
	map aliases = $[
	    "read" :	[ ``( ReadManagementDialog() ), true ],
	    "main"  :	``( MainSequence ("management") ),
	    "write" :	[ ``( WriteManagementDialog() ), true ],
	];

	map sequence = $[
	    "ws_start" : "read",
	    "read" : $[
		`abort	: `abort,
		`next	: "main",
	    ],
	    "main" : $[
		`abort	: `abort,
		`next	: "write",
	    ],
	    "write" : $[
		`abort	: `abort,
		`next	: `next,
	    ],
	];

	Wizard::CreateDialog();
	Wizard::DisableBackButton();
	Wizard::SetAbortButton(`abort, Label::CancelButton());
	Wizard::SetNextButton(`next, Label::OKButton());
	Wizard::SetTitleIcon ("yast-smt");

	any ret = Sequencer::Run(aliases, sequence);
	Wizard::CloseDialog();

	return ret;
    }

    any SMTInstallSequence () {
	map aliases = $[
	    "read"  : [ ``( ReadDialog() ), true ],
	    "main"  :   ``( MainInstallSequence() ),
	    "check" : [ ``( CheckConfigDialog() ), true ],
	    "write" : [ ``( WriteDialog() ), true ]
	];

	map sequence = $[
	    "ws_start" : "read",
	    "read" : $[
		`abort      : `abort,
		`next       : "main"
	    ],
	    "main" : $[
		`abort      : `abort,
		`next       : "write"
	    ],
	    "check" : $[
		`abort	: `abort,
		`next	: "write",
	    ],
	    "write" : $[
		`abort      : `abort,
		`next       : `next
	    ],
	];

	Wizard::SetTitleIcon ("yast-smt");
	Wizard::DisableBackButton();
	Wizard::SetAbortButton(`abort, Label::CancelButton());
	Wizard::SetNextButton(`next, Label::OKButton());

	any ret = Sequencer::Run (aliases, sequence);

	Wizard::RestoreNextButton();
	Wizard::RestoreAbortButton();
	Wizard::RestoreBackButton();

	return ret;
    }
}
