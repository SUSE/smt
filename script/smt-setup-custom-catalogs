#!/usr/bin/perl

use strict;
use warnings;
use SMT::CLI;
use File::Basename;
use SMT::Utils;
use Digest::SHA1  qw(sha1 sha1_hex);

use Locale::gettext ();
use POSIX ();     # Needed for setlocale()

POSIX::setlocale(&POSIX::LC_MESSAGES, "");


my $help;


my @productids = ();
my $catalogid = "";
my $name = "";
my $description = "";
my $exturl = "";
my $delete = "";

use Getopt::Long;
&Getopt::Long::Configure( 'pass_through', 'no_autoabbrev');
&Getopt::Long::GetOptions(
                          'productid=i'   => \@productids,
                          'name=s'        => \$name,
                          'description=s' => \$description,
                          'exturl=s'      => \$exturl,
                          'delete=s'      => \$delete,
                          'help|h'        => \$help
                         );

if ( defined $help and ( $help == 1 ) )
{
    print basename($0) . __(" : setup a custom catalog\n");
    print "    --productid arg     " . __("Product id this catalog belongs to.\n");
    print "                        " . __("You can use this option multiple times\n");
    print "                        " . __("to assign a catalog to multiple products \n");
    print "    --name  arg         " . __("The name of this catalog.\n");
    print "    --description arg   " . __("A description\n");
    print "    --exturl arg        " . __("The URL where this catalog can be mirrored from\n");
    print "    --delete arg        " . __("Remove a custom catalog with the given ID\n");
    print "                        " . __("Call 'smt catalogs --verbose' to find out the Catalog ID\n");
    print "                        " . __("of the catalog you want to delete.\n");
    print "\n\n";
    print __("Examples:\n\n");
    print "  smt-setup-custom-catalogs --productid 434 --name 'My_Catalog' --exturl 'http://my.domain.top/My_Catalog'\n";
    print "  smt-setup-custom-catalogs --delete 1cf336d819e8e5904f4d4b05ee081971a0cc8afc\n";
    
    exit 0;
}

if($delete ne "")
{
    print __(sprintf("Really delete the catalog with ID '%s' (y/n)?", $delete));

    my $yn = <STDIN>;
    chomp($yn);
    $yn =~ s/^\s*//;
    $yn =~ s/\s*$//;

    if(lc($yn) eq __("y"))
    {
        my $removed = SMT::CLI::removeCustomCatalog(catalogid => $delete);
        if(!$removed)
        {
            print __("No catalog removed.\n");
            exit 1;
        }
        else
        {
            print __(sprintf("Catalog with ID '%s' removed.\n", $delete));
        }
    }
    else
    {
        print __("Nothing deleted.\n");
    }
    exit 0;
}


my $dbh = SMT::Utils::db_connect();
if(!$dbh)
{
    die __("Cannot connect the database\n");
}


while($name !~ /^[a-zA-Z\d_-]+$/)
{
    print __("Invalid catalog name. Allowed characters are characters, numbers, - and _\n");
    
    print __("Catalog Name:");
    $name = <STDIN>;
}
chomp($name);

while($exturl !~ /^https?:\/\//)
{
    print __("Invalid URL. A http or https url is required.\n");
    
    print __("URL:");
    
    $exturl = <STDIN>;
}
chomp($exturl);

if($description eq "")
{
    print __("Catalog description:");
    
    $description = <STDIN>;
}
chomp($description);

my $array = $dbh->selectall_arrayref("SELECT PRODUCTDATAID, PRODUCT, VERSION, REL, ARCH from Products where PRODUCT_LIST='Y' group by PRODUCT, VERSION, REL, ARCH",
                                     {Slice => {}});
my @validProductIds = ();
my $anotherone = 0;

if(@productids == 0 )
{
    print __("Select a product where this catalog belong to.\n\n");

    foreach my $item (@{$array})
    {
        print $item->{PRODUCTDATAID}."\t";
        print $item->{PRODUCT}." ";
        print $item->{VERSION}." " if(defined $item->{VERSION});
        print $item->{REL}." " if(defined $item->{REL});
        print $item->{ARCH}." " if(defined $item->{ARCH});
        print "\n";

        push @validProductIds, $item->{PRODUCTDATAID};
    }
    print "\n";
}

while(@productids == 0 || $anotherone)
{
    print __("Product Number:");
    
    my $pid = <STDIN>;
    chomp($pid);
    
    if( !grep( ($_ eq $pid), @validProductIds) )
    {
        print __("Invalid Product Number.\n");
        next;
    }
    push @productids, $pid;
    
    print __("Select an additional product for this catalog(y/n)?");
    my $yn = <STDIN>;
    chomp($yn);
    
    if(lc($yn) eq __("y"))
    {
        $anotherone = 1;
    }
    else
    {
        $anotherone = 0;
    }
}
$catalogid = sha1_hex($name);

my %options = (
               productids  => \@productids,
               catalogid   => $catalogid,
               name        => $name,
               description => $description,
               exturl      => $exturl
              );

my $success = SMT::CLI::setupCustomCatalogs( %options );
if($success)
{
    print __("Catalog successfully added.\n");
    exit 0;
}
else
{
    print __("Adding catalog failed.\n");
    exit 1;
}

#
# Manpage
#

=head1 NAME

smt setup-custom-catalogs

=head1 SYNOPSIS

smt [help|--help|-h] setup-custom-catalogs 
smt setup-custom-catalogs --delete <catalogid>
smt-setup-custom-catalogs --productid <product_id> --name <catalog name> --exturl <url>

=head1 DESCRIPTION

The smt-setup-custom-catalogs script is a tool for setup custom catalogs 
(catalogs not present in NU) to be used with SMT. 
It can be used for adding a new catalog to the SMT database or to delete a catalog
from the database.


=head1 OPTIONS

=head2 

=over

=item --productid <id>

ID of a product the catalog belongs to. If the catalog should belong to multiple
products, use this option multiple times to assign catalog to all relevant products.

=item --name <catalog name>

The name of the custom catalog.

=item --description <description>

The description of the custom catalog.

=item --exturl <url>

The URL where this catalog can be mirrored from.

=item --delete <catalogID>

The command smt-setup-custom-catalogs --delete ID removes a
custom catalog with a given ID from the SMT database.

Call B<smt catalogs --verbose> to find out the Catalog ID of the catalog
you want to delete.

=back

=head1 EXAMPLES

Configure a new custom catalog:

 smt-setup-custom-catalogs --productid 431 --productid 432 --productid 433 --productid 434 --productid 435 \
 --name 'My_Catalog' --exturl 'http://my.domain.top/My_Catalog'


Remove the catalog with the id I<1cf336d819e8e5904f4d4b05ee081971a0cc8afc>:

  smt-setup-custom-catalogs --delete 1cf336d819e8e5904f4d4b05ee081971a0cc8afc

=head1 AUTHORS and CONTRIBUTORS

Duncan Mac-Vicar Prett, Lukas Ocilka, Jens Daniel Schmidt, Michael Calmer

=head1 LICENSE

Copyright (c) 2008 SUSE LINUX Products GmbH, Nuernberg, Germany.

This program is free software; you can redistribute it and/or modify it under
the terms of the GNU General Public License as published by the Free Software
Foundation; either version 2 of the License, or (at your option) any later
version.

This program is distributed in the hope that it will be useful, but WITHOUT ANY
WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE. See the GNU General Public License for more details.

You should have received a copy of the GNU General Public License along with
this program; if not, write to the Free Software Foundation, Inc., 675 Mass
Ave, Cambridge, MA 02139, USA.

=cut
             

