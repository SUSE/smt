#! /usr/bin/perl -w

use strict;
use English;
use SMT::Utils;
use SMT::CLI;
use SMT::Mirror::RegData;
use SMT::NCCRegTools;
use File::Basename;
use Config::IniFiles;

use Locale::gettext ();
use POSIX ();     # Needed for setlocale()

POSIX::setlocale(&POSIX::LC_MESSAGES, "");

if ($EFFECTIVE_USER_ID != 0)
{
    print "You must be root to run this program!\n";
    exit 1;
}

my $debug = 0;
my $vblevel = LOG_ERROR|LOG_WARN|LOG_INFO1;
my $logfile = "/var/log/smt/smt-scc-ncc-migration";
my $exitcode = 0;
my $help = 0;
my $check_only = 0;

use Getopt::Long;
Getopt::Long::Configure( 'no_auto_abbrev');
my $optres = Getopt::Long::GetOptions(
                                      "logfile|L=s" => \$logfile,
                                      'debug|d'     => \$debug,
                                      "verboselevel|v=i" => \$vblevel,
                                      'help|h'      => \$help,
                                      'check-only|c'=> \$check_only
                                     );

if ( (defined $help && $help == 1 ) || !$optres )
{
    print basename($0) . " : " . __("Migrate SMT from SCC to NCC.\n\n");
    print __("Usage:\n");
    print basename($0) . " [options]";
    print "\n\n";
    print __("Options:\n");
    print "--logfile -L file         ".__("Path to logfile\n");
    print "--debug                   ".__("Enable debug mode\n");
    print "--verboselevel -v level   ".__("set the verbose level\n");
    print "--check-only              ",__("Only check whether migration is possible\n");
    exit 0;
}

$vblevel = LOG_ERROR|LOG_WARN|LOG_INFO1|LOG_INFO2|LOG_DEBUG|LOG_DEBUG2 if($debug);

# open the logfile

my $LOG = SMT::Utils::openLog($logfile);

my $cfg = undef;

sub _writeConfig
{
    my $cfg = shift;
    my ($section, $key, $value) = @_;
    if(not $cfg->setval($section, $key, $value))
    {
        $cfg->newval($section, $key, $value);
    }
    $cfg->RewriteConfig();
    my ($login,$pass,$uid,$gid) = getpwnam("wwwrun");
    chown 0, $gid, "/etc/smt.conf";
}

sub _testDbSchema
{
    my $dbh = shift;

    my $v = $dbh->selectrow_hashref("SELECT version FROM migration_schema_version WHERE name = 'smt'");
    if(!$v || !$v->{version} || $v->{version} < 2.00)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, "Unable to migrate. DB schema missmatch.");
        SMT::Utils::unLockAndExit( "smt-ncc-scc-migration", 1, $LOG, $vblevel);
    }
}

eval
{
    $cfg = SMT::Utils::getSMTConfig();
};
if($@ || !defined $cfg)
{
    SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, sprintf(__("Cannot read the SMT configuration file: %s"), $@));
    exit 1;
}

my $dbh = SMT::Utils::db_connect($cfg);

if(!$dbh)
{
    SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR,  __("Cannot connect to database"));
    exit 1;
}

if(!SMT::Utils::openLock("smt-scc-ncc-migration"))
{
    print __("Migration process is still running.\n");
    exit 0;
}

_testDbSchema($dbh);

# re-write parts of the config file
my $regurl = $cfg->val('NU', 'NURegUrl');
my $regapi = $cfg->val('NU', 'ApiType', 'NCC');
if ( $regurl !~ /^https:\/\/scc\.suse\.com\/connect/ || $regapi ne 'SCC')
{
    SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, "Not connected to SCC");
    SMT::Utils::unLockAndExit( "smt-scc-ncc-migration", 1, $LOG, $vblevel);
}
_writeConfig($cfg, 'NU', 'NURegUrl', 'https://secure-www.novell.com/center/regsvc/');
_writeConfig($cfg, 'NU', 'ApiType', 'NCC');

eval
{
    my $nccsync = SMT::Mirror::RegData->new(vblevel => $vblevel,
                                            log     => $LOG,
                                            element => "productdata",
                                            table   => "Products",
                                            key     => ["PRODUCTLOWER", "VERSIONLOWER", "RELLOWER", "ARCHLOWER"]);
    $nccsync->migrate(1);
    printLog($LOG, $vblevel, LOG_INFO1, __("Checking if a migration is possible ..."));
    if(($exitcode = $nccsync->canMigrate()) > 0)
    {
        _writeConfig($cfg, 'NU', 'NURegUrl', $regurl);
        _writeConfig($cfg, 'NU', 'ApiType', 'SCC');
        SMT::Utils::unLockAndExit( "smt-scc-ncc-migration", $exitcode, $LOG, $vblevel);
    }

    if ( $check_only )
    {
        _writeConfig($cfg, 'NU', 'NURegUrl', $regurl);
        _writeConfig($cfg, 'NU', 'ApiType', 'SCC');
        SMT::Utils::unLockAndExit( "smt-ncc-scc-migration", 0, $LOG, $vblevel);
    }

    printLog($LOG, $vblevel, LOG_INFO1, __("Downloading Product information"));

    my $res = $nccsync->sync();
    if ($res)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, __("Error while fetching Products data."));
        $exitcode = 1;
    }

    $nccsync->element("targets");
    $nccsync->table("Targets");
    $nccsync->key("OS");

    printLog($LOG, $vblevel, LOG_INFO1, __("Downloading Target information"));
    $res = $nccsync->sync();
    if ($res)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, __("Error while fetching Targets data."));
        $exitcode = 1;
    }

    $nccsync->element("catalogs");
    $nccsync->table("Catalogs");
    $nccsync->key(['NAME', 'TARGET']);

    printLog($LOG, $vblevel, LOG_INFO1, __("Downloading repository information"));
    $res = $nccsync->sync();
    if ($res)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, __("Error while fetching repository data."));
        $exitcode = 1;
    }

    $nccsync->element("productcatalogs");
    $nccsync->table("ProductCatalogs");
    $nccsync->key(['PRODUCTID', 'CATALOGID']);

    printLog($LOG, $vblevel, LOG_INFO1, __("Downloading Product/Repository relations"));
    $res = $nccsync->sync();
    if ($res)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, __("Error while fetching ProductCatalogs data."));
        $exitcode = 1;
    }

    my $lr= SMT::NCCRegTools->new(vblevel => $vblevel,
                                  log     => $LOG);

    printLog($LOG, $vblevel, LOG_INFO1, __("Downloading Subscription information"));
    $res = $lr->NCCListSubscriptions();
    if ($res)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, __("Error while fetching Subscription data."));
        $exitcode = 1;
    }

    printLog($LOG, $vblevel, LOG_INFO1, __("Downloading Registration information"));
    $res = $lr->NCCListRegistrations();
    if ($res)
    {
        SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, __("Error while fetching Registration data."));
        $exitcode = 1;
    }

    printLog($LOG, $vblevel, LOG_INFO1, __("Flagged repositories which can be mirrored"));
    SMT::CLI::setMirrorableCatalogs(log     => $LOG,
                                    vblevel => $vblevel);

    if($cfg->val("LOCAL", "forwardRegistration", "true") eq "true")
    {
        printLog($LOG, $vblevel, LOG_INFO1, __("Schedule client registration"));
        $res = $dbh->do("UPDATE Registration SET NCCREGERROR = 0, REGDATE = CURRENT_TIMESTAMP");
    }

    printLog($LOG, $vblevel, LOG_INFO1, __("Cleanup Database"));
    $res = $dbh->do("DELETE FROM Products WHERE SRC='S'");
    $res = $dbh->do("DELETE FROM Catalogs WHERE SRC='S'");
    $res = $dbh->do("DELETE FROM ProductCatalogs WHERE SRC='S'");

};
if ($@)
{
    SMT::Utils::printLog($LOG, $vblevel, LOG_ERROR, $@);
    SMT::Utils::unLockAndExit( "smt-ncc-scc-migration", 1, $LOG, $vblevel);
}
SMT::Utils::unLockAndExit( "smt-ncc-scc-migration", $exitcode, $LOG, $vblevel);

